# Maintainer: Milk Brewster <milk on freenode>
_pkgname=raysession
pkgname=${_pkgname}-git
pkgver=r1968.d4b85cd
pkgrel=1
pkgdesc="Session manager for audio programs using the Non Session Manager (NSM) API"
arch=('x86_64' 'x86_64_v3')
url="https://github.com/Houston4444/RaySession"
license=('GPL')
groups=(pro-audio)
depends=('python-pyqt5' 'python-pyliblo' 'python-pyxdg')
makedepends=('git' 'qt5-tools')
provides=('raysession-git')
conflicts=('raysession')
source=('git+https://github.com/Houston4444/RaySession')
noextract=()
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/RaySession"

	# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/RaySession"
	git submodule update --init
	# git checkout tags/v0.13.1 -b v0.13.1
}

build() {
	cd "$srcdir/RaySession"
	make
	make locale
}

package() {
	cd "$srcdir/RaySession"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
