# Maintainer: Robert Pannick <rwpannick@gmail.com>
# Maintainer: Nigel Michki <nigeil@yahoo.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Serhii Balbieko <sergey@balbeko.com>
# Contributor: Simon Dreher <code@simon-dreher.de>

_name=sonic-pi
pkgname=sonic-pi-git
pkgver=v4.4.0.r74.gf487277ee
pkgrel=2
pkgdesc="The Live Coding Music Synth for Everyone"
arch=('i686' 'x86_64' 'x86_64_v3')
url="http://sonic-pi.net/"
license=('MIT')
groups=('pro-audio')
conflicts=('sonic-pi')
provides=('sonic-pi')
depends=('aubio' 'boost' 'cmake' 'elixir' 'erlang-nox' 'gcc-libs' 'gendesk'
'glfw-x11' 'glibc' 'lua' 'osmid' 'qscintilla-qt6' 'qt6-base' 'qt6-svg' 'qt6-tools'
'qt6-webengine' 'rtaudio' 'rtmidi' 'ruby' 'ruby-activesupport' 'ruby-bundler'
'ruby-ffi' 'ruby-i18n' 'ruby-kramdown' 'ruby-minitest' 'ruby-mocha' 'ruby-multi_json'
'ruby-rake' 'ruby-rouge' 'ruby-rugged' 'ruby-sys-proctable' 'ruby-rexml' 'stk' 'libxext' 'libxft' 'libx11' 'opus-tools' 'opus'
'sc3-plugins' 'sox' 'supercollider')
makedepends=('boost' 'curl' 'ninja' 'cmake' 'git' 'erlang-nox' 'gendesk' 'lua' 'qt5-tools' 'wkhtmltopdf-static' 'zip' 'unzip' 'tar' 'gcc12')
source=('git+https://github.com/samaaron/sonic-pi.git'
        "${_name}-4.4.0-gcc12.patch"
        "${_name}.sh")
sha512sums=('SKIP'
            '5af106fc5bbb4bc35c5cd3e90f5f6ac53569222a927f22e487db3a2ba88de9955370dcc7d3ede8791fbbf0a499d11787042a93667403358f78cafa114d9b3861'
            'bb578996b305fbb07b95610caf1fc1a08c760a49cf3902f012ed49ee4404f7d8c10d66aded88e538b20c811b4bb21c4e17f47761863eebd2bff2792ec38a738d')

pkgver() {
  cd "${_name}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g'  ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${_name}"
  git fetch && git checkout stable
  gendesk -n \
          --pkgname "${pkgname}" \
          --pkgdesc "${pkgdesc}" \
          --name "${_name}" \
          --exec "${_name}" \
          --categories "AudioVideo;Audio"

  # patch -Np1 -i "../${_name}-4.3.0-rugged_compile_flags.patch"
  #

  patch -Np1 -i "../${_name}-4.4.0-gcc12.patch"

}

build() {
  cd "${_name}/app"
  ./linux-prebuild.sh -n
  ./linux-config.sh -n -c Release
  ./linux-build-gui.sh -n -c Release
  ./linux-post-tau-prod-release.sh -n -c Release
}

## tests fail: https://github.com/samaaron/sonic-pi/issues/1865
#check() {
#  cd "${_name}"
#  cd app/server/ruby/test
#  rake test
#}

package() {
  cd "${_name}/app"

  ./linux-release.sh

  mkdir -p "${pkgdir}/opt/${_name}"

  cp -r build/linux_dist/* "${pkgdir}/opt/${_name}/"

  # xdg
  install -vDm 644 ../${_name}.desktop -t "${pkgdir}/usr/share/applications/"

  install -vDm 644 gui/qt/images/icon-smaller.png "${pkgdir}/usr/share/icons/${_name}.png"

  # license
  install -vDm 644 ../LICENSE.md -t "${pkgdir}/usr/share/licenses/${_name}/LICENSE"

  install -vDm 755 ../../${_name}.sh "${pkgdir}/usr/local/bin/${_name}"

}
