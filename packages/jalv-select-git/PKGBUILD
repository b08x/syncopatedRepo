# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>

_pkgname=jalv-select
pkgname="${_pkgname}-git"
pkgver=1.3.r140.29ea666
pkgrel=1
pkgdesc="A little GUI to select installed lv2 plug-ins and run them with jalv."
arch=('i686' 'x86_64' 'x86_64_v3')
url="https://github.com/brummer10/jalv_select"
license=('custom:Public Domain')
depends=('jalv' 'gtkmm' 'gtkmm3' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/brummer10/jalv_select.git")
md5sums=('SKIP')


pkgver() {
  cd "${srcdir}/${_pkgname}"

  local ver=$(grep "VER =" Makefile | awk '{print $3}')
  echo "$ver.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"

  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  make install DESTDIR="$pkgdir"

  # readme
  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/$pkgname/README.md"

  # license
  install -Dm644 "LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
